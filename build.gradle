apply plugin: 'java'
apply plugin: 'war'

sourceCompatibility = '1.7'
targetCompatibility = '1.7'

compileJava.options.encoding = 'UTF-8'
compileTestJava.options.encoding = 'UTF-8'

repositories {
    mavenCentral()
    maven { url 'http://public-maven.classmethod.info/release' }
    maven { url 'http://repository.jboss.org/nexus/content/groups/public' }
}

configurations {
    compileOnly
    jmockit
    arqCompile
    arqRuntime
    jbossManagedRuntime { extendsFrom arqRuntime }
    jbossRemoteRuntime  { extendsFrom arqRuntime }
}

dependencies {
    providedCompile 'org.jboss.spec:jboss-javaee-6.0:1.0.0.Final'
    providedCompile 'org.jboss.resteasy:resteasy-jaxrs:2.3.7.Final'
    
    compile 'com.mashape.unirest:unirest-java:1.3.20'
    compile 'org.slf4j:slf4j-log4j12:1.7.7'
    compile 'org.projectlombok:lombok:1.14.4'

    jmockit 'org.jmockit:jmockit:1.9'
    testCompile 'junit:junit:4.11'
    testCompile 'org.hamcrest:hamcrest-core:1.3'
    testCompile 'org.hamcrest:hamrest-library:1.3'
    testCompile 'jp.classmethod.testing:cmtest-db:0.4'
    testCompile 'org.yaml:snakeyaml:1.13'
    testCompile 'org.dbunit:dbunit:2.5.0'
    testCompile 'commons-io:commons-io:2.1'

    arqCompile 'org.jboss.shrinkwrap.descriptors:shrinkwrap-descriptors-api-javaee:2.0.0-alpha-5'
    arqRuntime 'org.jboss.shrinkwrap.descriptors:shrinkwrap-descriptors-impl-javaee:2.0.0-alpha-5'
    arqRuntime 'org.jboss.spec:jboss-javaee-6.0:1.0.0.Final'

    jbossManagedRuntime 'org.jboss-as-arquillian-container-managed:7.2.0.Final'
    jbossRemoteRuntime  'org.jboss-as-arquillian-container-remote:7.2.0.Final'
}

sourceSets {
    test {
        // jmockit を jUnitより先行させる
        compileClasspath = configurations.jmockit + compileClasspath
	runtimeClasspath = configurations.jmockit + runtimeClasspath
    }
    integTest {
        compileClasspath = configurations.arqCompile + compileClasspath
	runtimeClasspath = configurations.arqRuntime + runtimeClasspath
    }
}

apply plugin: 'jacoco'
jacoco {
    toolVersion = '0.7.1.201405082137'
}

jacocoTestReport {
    reports {
        csv.enabled false
	html.destination 'build/reports/coverage'
    }
}

test {
    // Enclosedテストが 2回実行される現象の回避
    exclude '**/*$*'
}

task jbossManagedTest(type: Test, dependsOn: integTestClasses) {
    testClassesDir = sourceSets.integTest.output.classesDir
    classpath = classpath + configurations.arqRuntime + configurations.jbossManagedRuntime

    reports.html.destination = file('build/reports/integration_test/report')
    reports.junitXml.destination = file('build/reports/integration_test/result')
}

task wrapper(type: Wrapper) {
    gradleVersion = '1.12'
}
